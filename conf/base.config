/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    nf-core/bacass Nextflow base config file
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Tuned for DTU HPC — targets the 47 smallest nodes (20 cores, 128 GB RAM).
    All resources are capped to never exceed node limits, even on retry.
----------------------------------------------------------------------------------------
*/

// Hard ceilings — fit the 47 smallest nodes (20-core / 128 GB Huawei XH620 V3 2660v3)
// Memory capped at 120 GB to leave headroom for OS on 128 GB nodes
params.max_cpus   = 20
params.max_memory = 120.GB
params.max_time   = 48.h

// Helper function to enforce ceilings
def check_max(obj, type) {
    if (type == 'memory') {
        try {
            if (obj.compareTo(params.max_memory as nextflow.util.MemoryUnit) == 1)
                return params.max_memory as nextflow.util.MemoryUnit
            else
                return obj
        } catch (all) {
            println "WARNING: Max memory '${params.max_memory}' is not valid. Using default."
            return obj
        }
    } else if (type == 'time') {
        try {
            if (obj.compareTo(params.max_time as nextflow.util.Duration) == 1)
                return params.max_time as nextflow.util.Duration
            else
                return obj
        } catch (all) {
            println "WARNING: Max time '${params.max_time}' is not valid. Using default."
            return obj
        }
    } else if (type == 'cpus') {
        try {
            return Math.min(obj as int, params.max_cpus as int)
        } catch (all) {
            println "WARNING: Max CPUs '${params.max_cpus}' is not valid. Using default."
            return obj
        }
    }
}

process {

    cpus   = { check_max( 1      * task.attempt, 'cpus'   ) }
    memory = { check_max( 6.GB   * task.attempt, 'memory' ) }
    time   = { check_max( 4.h    * task.attempt, 'time'   ) }

    errorStrategy = { task.exitStatus in ((130..145) + 104 + 175) ? 'retry' : 'finish' }
    maxRetries    = 1
    maxErrors     = '-1'

    // Process-specific resource requirements
    // NOTE - Please try and reuse the labels below as much as possible.
    //        These labels are used and recognised by default in DSL2 files hosted on nf-core/modules.
    //        If possible, it would be nice to keep the same label naming convention when
    //        adding in your local modules too.
    // See https://www.nextflow.io/docs/latest/config.html#config-process-selectors

    //                          Attempt 1          Attempt 2 (retry)
    //                          ----------         -----------------
    // process_single           1 CPU / 6 GB       1 CPU / 12 GB
    // process_low              4 CPU / 16 GB      8 CPU / 32 GB
    // process_medium           8 CPU / 40 GB      16 CPU / 80 GB
    // process_high             16 CPU / 40 GB     20 CPU / 80 GB
    // process_high_memory      (inherited)        120 GB           (capped)

    withLabel:process_single {
        cpus   = { check_max( 1                    , 'cpus'   ) }
        memory = { check_max( 6.GB  * task.attempt , 'memory' ) }
        time   = { check_max( 4.h   * task.attempt , 'time'   ) }
    }
    withLabel:process_low {
        cpus   = { check_max( 4     * task.attempt , 'cpus'   ) }
        memory = { check_max( 16.GB * task.attempt , 'memory' ) }
        time   = { check_max( 4.h   * task.attempt , 'time'   ) }
    }
    withLabel:process_medium {
        cpus   = { check_max( 8     * task.attempt , 'cpus'   ) }
        memory = { check_max( 40.GB * task.attempt , 'memory' ) }
        time   = { check_max( 8.h   * task.attempt , 'time'   ) }
    }
    withLabel:process_high {
        cpus   = { check_max( 16    * task.attempt , 'cpus'   ) }
        memory = { check_max( 40.GB * task.attempt , 'memory' ) }
        time   = { check_max( 16.h  * task.attempt , 'time'   ) }
    }
    withLabel:process_long {
        time   = { check_max( 20.h  * task.attempt , 'time'   ) }
    }
    withLabel:process_high_memory {
        memory = { check_max( 128.GB * task.attempt, 'memory' ) }
    }
    withLabel:error_ignore {
        errorStrategy = 'ignore'
    }
    withLabel:error_retry {
        errorStrategy = 'retry'
        maxRetries    = 2
    }
    withLabel: process_gpu {
        ext.use_gpu = { workflow.profile.contains('gpu') }
        accelerator = { workflow.profile.contains('gpu') ? 1 : null }
    }
}
